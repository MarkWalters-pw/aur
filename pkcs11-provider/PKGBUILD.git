pkgname=pkcs11-provider
_commit=$(git -C ~/src/security/pkcs11-provider rev-parse HEAD)
pkgver=0.3.r56.g57804f2
pkgrel=1
pkgdesc="OpenSSL 3.0 provider for PKCS#11 hardware-backed private keys"
url="https://github.com/latchset/pkcs11-provider"
arch=(x86_64)
license=(Apache-2.0)
depends=(
  "openssl>=3.0.7"
  p11-kit
)
makedepends=(
  gcc
  git
  meson
)
checkdepends=(
  expect
  gnutls
  nss
  opensc
  softhsm
)
source=("git+https://github.com/latchset/pkcs11-provider#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed "s/^v//; s/-/.r/; s/-/./"
}

build() {
  cd $pkgname
  meson setup build --wipe --prefix=/usr --sysconfdir=/etc
  meson compile -C build
}

check() {
  cd $pkgname
  meson test -C build
}

package() {
  cd $pkgname
  meson install -C build --destdir="$pkgdir"
  install -Dm644 README.md "$pkgdir"/usr/share/doc/pkcs11-provider/README.md
}

# vim: ts=2:sw=2:et
